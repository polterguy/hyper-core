/*
 * This is our main "landing page" for our Hyper Core help files.
 */
h2
  innerValue:Hyper Core

img
  style:"margin-left:1rem; float:right;max-width: 25%;"
  src:"https://phosphorusfive.files.wordpress.com/2018/03/earth_layers_model.png"

p
  innerValue:@"Hyper Core is a generic HTTP REST backend. It provides pre-fabricated sinks to interact with MySQL,
but there is nothing preventing you from using any database you want with it, or using it for other
things than accessing a database. Parts of this documentation requires basic understanding of Hyperlambda."

p
  innerValue:@"With Hyper Core you can completely ditch the ideas of <em>'Managed Ajax'</em> from Phosphorus Five,
and entirely choose your own front end - Such as e.g. AngularJS or ReactJS to build your front end. This (might)
provide a more scalable way for you to create your web apps, and also allows you to more easily create
JSON web services."


/*
 * All files at "root" of sub section.
 */
container
  element:ul
  oninit

    /*
     * Retrieving all other Hyperlambda files in the help folder.
     */
    list-files:@HYPER-CORE/help-files/Hyper Core/
      filter:.hl|.md

    /*
     * Removing "index.hl".
     */
    set:x:@"/@list-files/*/"":regex:/index.hl$/"""

    /*
     * Looping through each Hyperlambda file (except of course "index.hl").
     */
    for-each:x:/@list-files/*

      /*
       * Figuring out filename, without extension, and removing numbering parts.
       */
      split:x:/@_dp/#?name
        =:/
        =:.
      replace:x:/@split/0/-2?name
        src:regex:/[0-9]+ - /

      /*
       * Creating a list item, containing a hyperlink button, which once clicked,
       * loads and displays the specified help file.
       */
      create-widget
        parent:x:/../*/_event?value
        element:li
        widgets
          a
            href:#
            innerValue:x:/@replace?value
            .file:x:/@_dp/#?name
            onclick

              /*
               * Retrieving file name, loading and displaying file, by invoking common
               * widget lambda event, which does the heavy lifting for us.
               */
              get-widget-property:x:/../*/_event?value
                .file
              desktop.help.display-file:x:/@get-widget-property/*/*?value
