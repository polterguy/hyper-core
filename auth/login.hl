/*
 * HTTP REST request URL handler for logging in a user.
 *
 * Expects the current request being an HTTP POST request, and username and password
 * given as a URL encoded parameters. Will create a cookie, and return to
 * client, which you somehow need to forward in your consecutive requests.
 */


/*
 * Sanity checking HTTP method.
 */
p5.web.request.get-method
if:x:/@p5.web.request.get-method?value
  !=:POST
  throw:This REST service requires you to use a POST HTTP method for your request.


/*
 * Retrieving username and password.
 */
.defaults
  persist:bool:false
.args
  username
  password
  persist
p5.web.post.get:x:/@.args/*?name
login
  username:x:/@p5.web.post.get/*/username?value
  password:x:/@p5.web.post.get/*/password?value
  persist:x:(/@p5.web.post.get/*/persist|/@.defaults/*/persist)/$?value.bool


/*
 * Making sure we accurately set the "Content-Type" of our HTTP response, before we echo our
 * JSON results back to caller.
 */
p5.web.header.set:Content-Type
  src:application/json


/*
 * Echoing the role user belongs to back to caller.
 */
whoami
p5.web.echo:@"{{""role"":""{0}""}}"
  :x:/@whoami/*/role?value
